load("@aspect_rules_js//js:defs.bzl", "js_binary", "js_image_layer")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")

package(default_visibility = ["//visibility:public"])

ts_project(
    name = "lib",
    srcs = ["index.ts"],
    declaration = True,
    tsconfig = "//:tsconfig",
)

js_binary(
    name = "main",
    data = ["lib"],
    entry_point = "index.js",
)

js_image_layer(
    name = "layers",
    binary = ":main",
    root = "/app",
    visibility = ["//visibility:__pkg__"],
)

oci_image(
    name = "image",
    base = "@debian",
    cmd = ["/app/app/main"],
    entrypoint = ["bash"],
    tars = [":layers"],
    visibility = ["//visibility:public"],
)

platform(
    name = "linux_amd64",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

platform(
    name = "linux_arm64",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:arm64",
    ],
)

platform_transition_filegroup(
    name = "transitioned_image",
    srcs = [":image"],
    target_platform = select({
        "@platforms//cpu:arm64": ":linux_arm64",
        "@platforms//cpu:x86_64": ":linux_amd64",
    }),
)

oci_tarball(
    name = "image_tar",
    image = ":transitioned_image",
    repo_tags = ["rules_oci_debian_repro:latest"],
    visibility = ["//visibility:public"],
)

load("@aspect_rules_js//js:defs.bzl", "js_binary", "js_image_layer")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")

package(default_visibility = ["//visibility:public"])

ts_project(
    name = "lib",
    srcs = ["index.ts"],
    declaration = True,
    tsconfig = "//:tsconfig",
)

js_binary(
    name = "main",
    data = ["lib"],
    entry_point = "index.js",
)

js_image_layer(
    name = "layers",
    binary = ":main",
    root = "/app",
    visibility = ["//visibility:__pkg__"],
)

platform(
    name = "amd64_linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

platform_transition_filegroup(
    name = "transitioned_layers",
    srcs = [":layers"],
    target_platform = ":amd64_linux",
)

oci_image(
    name = "image",
    architecture = "amd64",
    # base = "@debian//:image",
    base = "@debian",
    cmd = ["/app/app/main"],
    entrypoint = ["bash"],
    os = "linux",
    tars = [":transitioned_layers"],
    visibility = ["//visibility:public"],
)

oci_tarball(
    name = "image_tar",
    image = ":image",
    repotags = ["rules_oci_debian_repro:latest"],
    visibility = ["//visibility:public"],
)
